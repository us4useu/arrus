cmake_minimum_required(VERSION 3.17.0)

project(arrus LANGUAGES CXX)
set(CMAKE_CXX_STANDARD 17)
# Version
set(PROJECT_VERSION 0.5.11)

option(ARRUS_DEVELOP_VERSION "Build develop version." ON)
if(ARRUS_DEVELOP_VERSION)
    set(PROJECT_FULL_VERSION "${PROJECT_VERSION}-dev")
else()
    set(PROJECT_FULL_VERSION "${PROJECT_VERSION}")
endif()

string(TIMESTAMP CURRENT_YEAR "%Y")

################################################################################
# Modules
################################################################################
set(CMAKE_MODULE_PATH
    "${PROJECT_SOURCE_DIR}/cmake"
    ${CMAKE_BINARY_DIR} # Includes cmake scripts generated by conan package
    ${CMAKE_MODULE_PATH})

################################################################################
# Global options and settings
################################################################################
include(common)

set(ARRUS_ROOT_DIR ${CMAKE_SOURCE_DIR})
set(ARRUS_DOCS_INSTALL_DIR docs)
set(ARRUS_MATLAB_INSTALL_DIR matlab)
set(ARRUS_PYTHON_INSTALL_DIR python)

set(Us4_LIB_DIR ${Us4_ROOT_DIR}/lib64)

option(ARRUS_BUILD_PY "Build python API." OFF)
option(ARRUS_BUILD_MATLAB "Build MATLAB API." OFF)
option(ARRUS_BUILD_DOCS "Build documentation." OFF)
option(ARRUS_RUN_TESTS "Run all tests builded packages." OFF)
option(ARRUS_EMBED_DEPS "Embed dependencies (like us4r dlls) into the output package." OFF)

option(ARRUS_BUILD_SWIG "Build swig wrappers. \
NOTE: this should turned on in order to build proper API. Turn it off \
only for testing and development, for example to work on currently unsupported \
platforms." ON)
if(NOT ARRUS_BUILD_SWIG)
    message(WARNING "BUILDING PACKAGE WITHOUT LOW-LEVEL API WRAPPERS!")
endif()

# Determining host platform.
if(MSVC)
    set(ARRUS_BUILD_PLATFORM windows)
elseif(UNIX AND NOT APPLE)
    set(ARRUS_BUILD_PLATFORM linux)
else()
    message(FATAL_ERROR "Unsupported platform.")
endif()

# Common C++ compile options
if("${ARRUS_BUILD_PLATFORM}" STREQUAL "windows")
    # permissive- is required by range-v3/0.5.0
    set(ARRUS_CPP_COMMON_COMPILE_OPTIONS /permissive- /EHsc)
    set(ARRUS_CPP_STRICT_COMPILE_OPTIONS "/W4 /WX")

    set(ARRUS_CPP_COMMON_COMPILE_DEFINITIONS
            _WIN32
            # warnings occurs in boost bimap implementation headers
            _SILENCE_CXX17_ALLOCATOR_VOID_DEPRECATION_WARNING
            _CRT_SECURE_NO_WARNINGS)
else("${ARRUS_BUILD_PLATFORM}" STREQUAL "linux")
    set(ARRUS_CPP_COMMON_COMPILE_OPTIONS "")
    set(ARRUS_CPP_STRICT_COMPILE_OPTIONS "-Wall -Wextra -pedantic -Werror")
    set(ARRUS_CPP_COMMON_COMPILE_DEFINITIONS
            ARRUS_LINUX
            _SILENCE_CXX17_ALLOCATOR_VOID_DEPRECATION_WARNING)
endif()

# installation directories
set(ARRUS_BIN_INSTALL_DIR bin)
set(ARRUS_LIB_INSTALL_DIR lib64)
set(ARRUS_INCLUDE_INSTALL_DIR include)
set(ARRUS_DOCS_INSTALL_DIR docs)

################################################################################
# Common dependencies
################################################################################
if(ARRUS_BUILD_SWIG)
    find_package(Us4 0.5.4 EXACT REQUIRED US4OEM HV256 DBARLite)
endif()
find_package(Boost REQUIRED)
set(Boost_USE_STATIC_LIBS ON)
find_package(Protobuf REQUIRED)
# Use project root to search for .proto files.
set(Protobuf_IMPORT_DIRS ${CMAKE_CURRENT_SOURCE_DIR})
find_package(fmt REQUIRED)
find_package(Microsoft.GSL REQUIRED)
find_package(Eigen3 REQUIRED)
################################################################################
# Sub-projects
################################################################################
if(ARRUS_RUN_TESTS)
    include(tests)
    enable_testing()
endif()

add_subdirectory(arrus/core)

if(ARRUS_BUILD_PY)
    add_subdirectory(api/python)
endif()
if(ARRUS_BUILD_MATLAB)
    add_subdirectory(api/matlab)
endif()
if(ARRUS_BUILD_DOCS)
    add_subdirectory(docs)
endif()
add_subdirectory(installer)

################################################################################
# General docs
################################################################################
configure_file(VERSION.rst.in VERSION.rst)
install(
        FILES ${CMAKE_CURRENT_BINARY_DIR}/VERSION.rst
        DESTINATION .
)
################################################################################
# Embed external dependencies
################################################################################
if(ARRUS_EMBED_DEPS)
    # TODO Remove transitive dependency on boost (embed deps directly).
    message("Us4r API ROOT DIR: ${Us4_ROOT_DIR}")
    # Sanitize provided path.
    string(REPLACE "\\" "/" Us4_ROOT_DIR_SANITIZED ${Us4_ROOT_DIR})

    install(
        DIRECTORY
            ${Us4_ROOT_DIR_SANITIZED}/lib64/
        DESTINATION
            ${ARRUS_LIB_INSTALL_DIR}
    )
    install(
       FILES
            # TODO(pjarosik) make it more general (will not work for OS!=win)
            ${Us4_ROOT_DIR_SANITIZED}/matlab/Us4MEX.mexw64
       DESTINATION
            matlab/arrus
    )
    install(
        FILES
            # TODO(pjarosik) make it more general (will not work for OS!=win)
            ${Us4_ROOT_DIR_SANITIZED}/bin/us4OEMFirmwareUpdate.exe
        DESTINATION
            ${ARRUS_BIN_INSTALL_DIR}
    )
endif()

