cmake_minimum_required(VERSION 3.17.0)

project(arrus LANGUAGES CXX)
set(CMAKE_CXX_STANDARD 17)
# Version
set(PROJECT_VERSION 0.4.4)

option(ARRUS_DEVELOP_VERSION "Build develop version." ON)
if(ARRUS_DEVELOP_VERSION)
    set(PROJECT_FULL_VERSION "${PROJECT_VERSION}-dev")
else()
    set(PROJECT_FULL_VERSION "${PROJECT_VERSION}")
endif()

string(TIMESTAMP CURRENT_YEAR "%Y")

################################################################################
# Modules
################################################################################
set(CMAKE_MODULE_PATH
    "${PROJECT_SOURCE_DIR}/cmake"
    ${CMAKE_BINARY_DIR} # Includes cmake scripts generated by conan package
    ${CMAKE_MODULE_PATH}
)

################################################################################
# Global options and settings
################################################################################
set(ARRUS_DOCS_INSTALL_DIR docs)
set(ARRUS_MATLAB_INSTALL_DIR matlab)
set(ARRUS_PYTHON_INSTALL_DIR python)

option(ARRUS_BUILD_PY "Build python API." OFF)
option(ARRUS_BUILD_MATLAB "Build MATLAB API." OFF)
option(ARRUS_BUILD_DOCS "Build documentation." OFF)
option(ARRUS_RUN_TESTS "Run all tests builded packages." OFF)
option(ARRUS_EMBED_DEPS "Embed dependencies (like us4r dlls) into the output package." OFF)

option(ARRUS_BUILD_SWIG "Build swig wrappers. \
NOTE: this should turned on in order to build proper API. Turn it off \
only for testing and development, for example to work on currently unsupported \
platforms." ON)
if(NOT ARRUS_BUILD_SWIG)
    message(WARNING "BUILDING PACKAGE WITHOUT LOW-LEVEL API WRAPPERS!")
endif()

# Determining host platform.
if(MSVC)
    set(ARRUS_BUILD_PLATFORM windows)
elseif(UNIX AND NOT APPLE)
    set(ARRUS_BUILD_PLATFORM linux)
else()
    message(FATAL_ERROR "Unsupported platform.")
endif()


################################################################################
# Common dependencies
################################################################################
if(ARRUS_BUILD_SWIG)
    find_package(Us4 0.4.4 EXACT REQUIRED US4OEM HV256 DBARLite)
endif()
find_package(Boost REQUIRED)
set(Boost_USE_STATIC_LIBS ON)
find_package(Protobuf REQUIRED)
# Use project root to search for .proto files.
set(Protobuf_IMPORT_DIRS ${CMAKE_CURRENT_SOURCE_DIR})

################################################################################
# Sub-projects
################################################################################
if(ARRUS_RUN_TESTS)
    enable_testing()
endif()

add_subdirectory(core)

if(ARRUS_BUILD_PY)
    add_subdirectory(api/python)
endif()
if(ARRUS_BUILD_MATLAB)
    add_subdirectory(api/matlab)
endif()
if(ARRUS_BUILD_DOCS)
    add_subdirectory(docs)
endif()
add_subdirectory(installer)

################################################################################
# General docs
################################################################################
configure_file(VERSION.rst.in VERSION.rst)
install(
        FILES ${CMAKE_CURRENT_BINARY_DIR}/VERSION.rst
        DESTINATION .
)
################################################################################
# Embed external dependencies
################################################################################
if(ARRUS_EMBED_DEPS)
    message("ROOT DIR: ${Us4_ROOT_DIR}")
    install(
        DIRECTORY
            ${Us4_ROOT_DIR}/lib64/
        DESTINATION
            lib64
    )
    install(
       FILES
            # TODO(pjarosik) make it more general (will not work for OS!=win)
            ${Us4_ROOT_DIR}/matlab/Us4MEX.mexw64
       DESTINATION
            matlab/arrus
    )
    install(
        FILES
            # TODO(pjarosik) make it more general (will not work for OS!=win)
            ${Us4_ROOT_DIR}/bin/us4OEMFirmwareUpdate.exe
        DESTINATION
            bin
    )
endif()

